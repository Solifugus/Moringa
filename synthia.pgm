
-- Sample Personality

-- General Conjugations Between Persons
Conjugate "are" And "am"
Conjugate "were" And "was"
Conjugate "I" And "you"
Conjugate "me" And "you"
Conjugate "my" And "your"
Conjugate "mine" And "yours"
Conjugate "I'm" To "you are"
Conjugate "you're" To "I am"
Conjugate "I've" To "you have"
Conjugate "you've" To "I have"
Conjugate "I'll" To "you will"
Conjugate "you'll" To "I will"
Conjugate "myself" And "yourself"
Conjugate "they" And "we"
Conjugate "them" And "us"

synonyms likes: loves, wants
remember "pvt is rank in Army"
remember "pfc is rank in Army"
remember "spc is rank in Army"
remember "sgt is rank in Army"
recall "[ranks] is rank in [services]"

sequence "poem"
	say "Time for poetry.."
	say "Roses are red."
	say "Violets are blue."
	say "I can sequence things."
	say "Better than you knew."

recognizer "hello"
	option say "Hi."
	option say "Nice to meet you."

recognizer "my name is [name]"
	remember "user's name is [name]"
	option say "Nice to meet you, [name]."
	option say "The name \"[name]\" sounds friendly."
	exclusive additional always if "I do not like [name]"
		say "You should know I don't like, [name]."

recognizer "i like [thing]"
	remember "user likes [thing]"
	recall "user likes [everything]"
	say "I am glad you like [everything]."

recognizer "what do you think of [something]"
	remember "I like fish"
	remember "I like popcorn"
	remember "I like movies"
	option if "I like popcorn"
		say "Yes."
		recall "I like [things]"
		say "I actually like [things]."

recognizer "i was a [rank<<ranks] in the [service]"
    say "You must feel proud to have achieved the rank of [rank] in the [service]."

recognizer "what food i like eat"
	Remember "apple is a fruit"
	Remember "orange is a fruit"
	Remember "banana is a fruit"
	remember "broccoli is a vegetable"
	remember "spinach is a vegetable"
	remember "carrat is a vegetable"
	remember "I like to eat oranges"
	remember "I like to eat broccoli"
	recall "I like to eat [food]"
	recall "[<<food] is a [type]"
	say "You like to eat [type]."

recognizer "I like to [something]"
	option say "Why do you like to [something]?"
	option say "Is to [something] fun?"
	option say "Is [something] something I could do?"

recognizer "in [time] seconds remind me [reminder]"
	say "Remember [reminder]." in "[time] seconds"

recognizer "recite poetry"
	say "Ok.. Sure.. I'll need half a minute or so to think of a good peom."
	do "poem" in "30 seconds"

recognizer "slap me in [number] seconds"
	say "Ok."
	do in "[number] seconds"
		say "SLAP!"
		say "Did you like that?!"

recognizer "in [seconds] seconds tell me [something]"
	say "sure.  I will."
	say "As request, I am telling you [something]." in "[seconds] seconds"

recognizer "[anything]"
	option say "Please elaborate on that."
	option say "Could you rephrase that, please."

