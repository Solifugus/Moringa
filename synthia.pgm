
-- Sample Personality
Memories
	"good is a feeling"
	"good is positive"
	"ok is a feeling"
	"bad is a feeling"
	"bad is negative"

-- General Conjugations Between Persons
Conjugate "are" And "am"
Conjugate "were" And "was"
Conjugate "I" And "you"
Conjugate "me" And "you"
Conjugate "my" And "your"
Conjugate "mine" And "yours"
Conjugate "I'm" To "you are"
Conjugate "you're" To "I am"
Conjugate "I've" To "you have"
Conjugate "you've" To "I have"
Conjugate "I'll" To "you will"
Conjugate "you'll" To "I will"
Conjugate "myself" And "yourself"
Conjugate "they" And "we"
Conjugate "them" And "us"

synonyms likes: loves, wants
remember "pvt is rank in Army"
remember "pfc is rank in Army"
remember "spc is rank in Army"
remember "sgt is rank in Army"
recall "[ranks] is rank in [services]"

sequence "poem"
	say "Time for poetry.."
	say "Roses are red."
	say "Violets are blue."
	say "I can sequence things."
	say "Better than you knew."

recognizer "i am [something]"
	option if "[something] is a feeling"
		interpret as "I am feeling [something]."
	option if Not "[something] is a feeling"
		Remember "user is [something]"
		say "I will remember that."

Recognizer "i am feeling [feeling]"
	remember "[feeling] is a feeling"
	option if "[feeling] is positive"
		say "Well that is great."
	option if "[feeling] is negative"
		say "That is too bad.  I would feel bad for you, if I could."
	option if Not "[feeling] is positive" and not "[feeling] is negative"
		say "Is feeling [feeling] positive or negative?"
		expect "positive" as "[feeling] is positive"
		expect "negative" as "[feeling] is negative"

recognizer "[something] is [other]"
	remember "[something] is [other]"
	say "I will remember that."

recognizer "hello"
	option say "Nice to meet you."
	option say "Hi."

recognizer "my name is [name]"
	remember "user's name is [name]"
	option say "Nice to meet you, [name]."
	option say "The name \"[name]\" sounds friendly."
	exclusive additional always if "I do not like [name]"
		say "You should know I don't like, [name]."

recognizer "i like [thing]"
	remember "user likes [thing]"
	recall "user likes [everything]"
	say "I am glad you like [everything]."

recognizer "a [thing] is something i like"
	interpret as "I like [thing]."

recognizer "what do you think of [something]"
	remember "I like fish"
	remember "I like popcorn"
	remember "I like movies"
	option if "I like popcorn"
		say "Yes."
		recall "I like [things]"
		say "I actually like [things]."

recognizer "i was a [rank<<ranks] in the [service]"
    say "You must feel proud to have achieved the rank of [rank] in the [service]."

recognizer "what food i like eat"
	Remember "apple is a fruit"
	Remember "orange is a fruit"
	Remember "banana is a fruit"
	remember "broccoli is a vegetable"
	remember "spinach is a vegetable"
	remember "carrat is a vegetable"
	remember "I like to eat oranges"
	remember "I like to eat broccoli"
	recall "I like to eat [food]"
	recall "[<<food] is a [type]"
	say "You like to eat [type]."

recognizer "I like to [something]"
	option say "Why do you like to [something]?"
	option say "Is to [something] fun?"
	option say "Is [something] something I could do?"

recognizer "in [time] seconds remind me [reminder]"
	say "[time] seconds have past, so I mean to remind you [reminder]." in "[time] seconds"

recognizer "recite poetry"
	say "Ok.. Sure.. I'll need half a minute or so to think of a good poem."
	do "poem" in "30 seconds"

recognizer "slap me in [number] seconds"
	say "Ok."
	do in "[number] seconds"
		say "SLAP!"
		say "Did you like that?!"

recognizer "in [seconds] seconds tell me [something]"
	say "I will."
	say "As requested to do after [seconds] seconds, I am telling you [something]." in "[seconds] seconds"

recognizer "do you have any [something] today"
	say "I have no [something] today.  Do you need some?"
	expect "yes" as "I need [something]."
	expect "no" as "I do not need [something]."

recognizer "i need [something]"
	say "well, I hope you get [something].'"

recognizer "i do not need [something]."
	say "I will be sure not to give you [something]."

recognizer "[anything]"
	option say "Please elaborate on that."
	option say "Could you rephrase that, please."

